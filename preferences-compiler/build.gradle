apply plugin: 'java-library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'

dependencies {
	implementation fileTree(dir: 'libs', include: ['*.jar'])
	implementation project(path: ':preferences-annotations')

	implementation 'com.squareup:kotlinpoet:1.0.1'

	implementation("com.google.auto.service:auto-service:1.0-rc2")
	kapt("com.google.auto.service:auto-service:1.0-rc2")
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

ext.artifact = [
		'groupId': 'ru.starksoft',
		'id'     : 'preferences-compiler',
		'version': '1.1.0',
		'siteUrl': 'https://github.com/Starksoft/auto-preference',
		'gitUrl' : 'https://github.com/Starksoft/auto-preference.git',
		'repo'   : 'android-maven'
]

version = artifact.version
group = artifact.groupId

install {
	repositories.mavenInstaller {
		pom.project {
			packaging 'jar'
			groupId artifact.groupId
			artifactId artifact.id
			version artifact.version
			name artifact.id // pom.project.name must be same as bintray.pkg.name
			url artifact.siteUrl
			inceptionYear '2018' // HARDCODED
			licenses {
				license { // HARDCODED
					name 'GPL-3.0'
					url 'https://www.gnu.org/licenses/gpl.txt'
					distribution 'repo'
				}
			}
			scm {
				connection artifact.gitUrl
				developerConnection artifact.gitUrl
				url artifact.siteUrl
			}
		}
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

bintray {
	user = System.getenv('user')
	key = System.getenv('key')

	configurations = ['archives']
	pkg {
		repo = artifact.repo
		name = artifact.id
		websiteUrl = artifact.siteUrl
		vcsUrl = artifact.gitUrl
		userOrg = artifact.userOrg
		licenses = ['Apache-2.0']
		publish = true
		version {
			name = artifact.version
		}
	}
}